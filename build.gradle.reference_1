Git URL: https://gitlab.com/Aristois/Installer/-/blob/master/build.gradle

/* Reference code */
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "edu.sc.seis.launch4j:launch4j:2.4.6"
        classpath "io.freefair.gradle:lombok-plugin:3.1.2"
    }
}

def donorBuild = project.hasProperty('donorbuild') ? project.property('donorbuild').toBoolean() : false

group 'me.deftware'
apply plugin: "io.freefair.lombok"
apply plugin: 'java'
apply plugin: "edu.sc.seis.launch4j"
version '2.0.1'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

configurations {
    extraLibs
    implementation.extendsFrom(extraLibs)
}

dependencies {
    extraLibs group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    extraLibs group: 'commons-io', name: 'commons-io', version: '2.6'
    extraLibs 'com.intellij:forms_rt:7.0.3'
    extraLibs group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4'
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest.attributes(
            "donorBuild": donorBuild,
            "build_date": new Date().format('yyyy/MM/dd HH:mm'),
            "Main-Class": 'me.deftware.aristois.installer.Main',
            "Manifest-Version": 1.0
    )
}

task prepareManifest(type: Copy) {
    from 'createExe/emc_installer_manifest.xml'
    into tasks.createExe.temporaryDir
    expand(
            artifact: project.name,
            version: project.version
    )
}

createExe {
    dependsOn 'prepareManifest'

    copyConfigurable = project.tasks.jar.outputs.files
    jar = "${projectDir}/build/libs/${project.tasks.jar.archiveFileName.get()}"
    icon = "${projectDir}/createExe/emc_installer_icon.ico"
    manifest = "${tasks.createExe.temporaryDir}/emc_installer_manifest.xml"
    outfile = project.tasks.jar.archiveFileName.get().replace(".jar", "") + ".exe"
    version = project.version
    internalName = project.name
    productName = "Installer"
    fileDescription = "EMC Installer - Release ${version}"
    copyright = "Copyright (C) 2018 - 2020 Aristois.net"
}

task copyToLibs(type: Copy) {
    dependsOn createExe

    from 'build/launch4j/' + project.tasks.jar.archiveFileName.get().replace(".jar", "") + ".exe"
    into 'build/libs/'
}

task copyToPackager(type: Copy) {
    dependsOn copyToLibs

    from 'build/libs/'
    into 'packager/' + (donorBuild ? "donor" : "free") + "/"
}

build.dependsOn(copyToPackager)
